ext {
    bintrayRepo = "chrynan"
    libraryGroup = "com.chrynan.kotlin-writer"
    libraryOwner = "chrynan"
    libraryRepo = "kotlin-writer"
    libraryVersion = "0.2.1"
    libraryVersionCode = 4
    libraryVersionDescription = "Second Major Release"
}

group libraryGroup
version libraryVersion

buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.1"
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.bintray"
    apply plugin: "org.jetbrains.dokka"

    dokka {
        outputFormat = "gfm"
        outputDirectory = "../docs"

        configuration {
            includeNonPublic = false
            skipEmptyPackages = true
            skipDeprecated = true
            reportUndocumented = true
            jdkVersion = 8
            targets = ["JVM", "COMMON"]
        }
    }

    bintray {
        user = project.findProperty("BINTRAY_USER") ?: System.getenv("BINTRAY_USER")
        key = project.findProperty("BINTRAY_KEY") ?: System.getenv("BINTRAY_KEY")
        pkg {
            repo = rootProject.bintrayRepo
            name = rootProject.libraryRepo
            licenses = ["Apache-2.0"]
            vcsUrl = "https://github.com/chRyNaN/kotlin-writer"
            version {
                name = rootProject.libraryVersion
                desc = rootProject.libraryVersionDescription
                released = new Date()
                vcsTag = rootProject.libraryVersion
            }
        }
    }

    bintrayUpload.doFirst {
        publications = publishing.publications.collect {
            it.name
        }
    }

    bintrayUpload.dependsOn publishToMavenLocal
}